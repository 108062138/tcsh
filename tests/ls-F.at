AT_BANNER([ls-F built-in command])

dnl
dnl	ls-F
dnl

dnl
dnl Note that the test groups don't test multiple columns
dnl because the terminal doesn't get setup by default with -c.
dnl

AT_SETUP([ls-F])
AT_KEYWORDS([commands])

AT_CHECK([create_ls_DIR])

dnl
dnl Disable CLICOLOR_FORCE, LSCOLORS, and LS_COLORS
dnl

unset CLICOLOR_FORCE
unset LSCOLORS
unset LS_COLORS

dnl
dnl ls-F with default behavior
dnl

AT_CHECK([tcsh -f -c 'cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-default.out], [stdout])

dnl
dnl listlinks.
dnl Changes from default:
dnl	ldir>
dnl	lldir>
dnl	llorphan&
dnl	lorphan&
dnl

AT_CHECK([tcsh -f -c 'set listlinks; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-listlinks.out], [stdout])

dnl
dnl listflags=x
dnl Note: no changes from default when in in 1 column mode
dnl

AT_CHECK([tcsh -f -c 'set listflags=x; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-listflags-x.out], [stdout])

dnl
dnl listflags=A
dnl Changes from default:
dnl	.hidden
dnl

AT_CHECK([tcsh -f -c 'set listflags=A; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-listflags-A.out], [stdout])

dnl
dnl listflags=xa
dnl Changes from default:
dnl	../
dnl	./
dnl	.hidden
dnl

AT_CHECK([tcsh -f -c 'set listflags=xa; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-listflags-xa.out], [stdout])

AT_CLEANUP()


dnl
dnl	ls-F color
dnl

AT_SETUP([ls-F color])
AT_KEYWORDS([commands])

AT_CHECK([create_ls_DIR])

dnl
dnl Enable CLICOLOR_FORCE.
dnl Disable LSCOLORS, and LS_COLORS.
dnl

CLICOLOR_FORCE=
export CLICOLOR_FORCE
unset LSCOLORS
unset LS_COLORS

dnl
dnl Default colors, listflags=A, listlinks
dnl

AT_CHECK([tcsh -f -c 'set color listflags=A listlinks; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-color-default.out], [stdout])

dnl
dnl LSCOLORS: dim foreground colors,
dnl except black for set*id and sticky are are not
dnl currently supported and not expected.
dnl

AT_CHECK([LSCOLORS=bxcxdxexfxgxhxaxaxaxax tcsh -f -c 'set color listflags=A listlinks; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-LSCOLORS-dim.out], [stdout])

dnl
dnl LSCOLORS: bright foreground colors,
dnl except bright black (dark grey) for set*id and sticky are are not
dnl currently supported and not expected.
dnl

AT_CHECK([LSCOLORS=BxCxDxExFxGxHxAxAxAxAx tcsh -f -c 'set color listflags=A listlinks; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-LSCOLORS-bright.out], [stdout])

dnl
dnl unsetenv CLICOLOR_FORCE
dnl Should be same as default ls-F without color.
dnl

AT_CHECK([tcsh -f -c 'set color; unsetenv CLICOLOR_FORCE; cd DIR && ls-F'], [0], [stdout])
TCSH_DIFF([${tests_dir}/ls-F-default.out], [stdout])

AT_CLEANUP()
